include "../../../../arch/x64/X64.Vale.InsBasic.vaf"
include "../../../../arch/x64/X64.Vale.InsMem.vaf"
include "../../../../arch/x64/X64.Vale.InsStack.vaf"
include{:fstar}{:open} "Fast_defs"
include{:fstar}{:open} "Fast_lemmas_external"
include{:fstar}{:open} "FastSqr_helpers"
include{:fstar}{:open} "X64.CPU_Features_s"

module X64.FastSqr

#reset-options "--z3rlimit 60"

#verbatim{:interface}
open Types_s
open Arch.Types
open X64.Machine_s
open X64.Memory
open X64.Stack_i
open X64.Vale.State
open X64.Vale.Decls
open X64.Vale.InsBasic
open X64.Vale.InsMem
open X64.Vale.InsStack
open X64.Vale.QuickCode
open X64.Vale.QuickCodes
open Fast_defs
open X64.CPU_Features_s
#endverbatim

#verbatim{:implementation}
open Types_s
open Arch.Types
open X64.Machine_s
open X64.Memory
open X64.Vale.State
open X64.Vale.Decls
open X64.Vale.InsBasic
open X64.Vale.InsMem
open X64.Vale.InsStack
open X64.Vale.QuickCode
open X64.Vale.QuickCodes
open FStar.Tactics
open Fast_defs
open Fast_lemmas_external
open FastSqr_helpers
open X64.CPU_Features_s
#endverbatim

type tactic:Type(0) extern;
const int_canon:tactic extern;
ghost procedure assert_by_tactic(ghost p:prop, ghost t:tactic) extern;

#reset-options "--z3rlimit 600"
procedure{:quick} fast_sqr_part1(
    inline offset:nat,
    ghost inA_b:buffer64)
    lets
        inA_ptr @= rsi;

        a0 := buffer64_read(inA_b, 0 + offset, mem);
        a1 := buffer64_read(inA_b, 1 + offset, mem);
        a2 := buffer64_read(inA_b, 2 + offset, mem);
        a3 := buffer64_read(inA_b, 3 + offset, mem);

        a := pow2_four(a0, a1, a2, a3);
    reads
        inA_ptr; mem; memTaint;

    modifies
        rax; rcx; rdx; r8; r9; r10; r11; r12; r13; r14; r15;
        efl;

    requires
        adx_enabled && bmi2_enabled;
        offset == 0 || offset == 4;

        validSrcAddrs64(mem, inA_ptr, inA_b, 4 + offset, memTaint, Secret);

    ensures
        pow2_six(r8, r9, r10, r11, r12, r13) ==
        pow2_five(mul_nats(a0,a1), mul_nats(a0,a2), mul_nats(a0,a3), mul_nats(a1,a3), mul_nats(a2,a3));

        pow2_64 * rcx + rax == a1 * a2;

        r14 == 0;
{
    xor_lemmas();

//    "movq   (%1), %%rdx        ;" /* A[0]      */
//    "mulx  8(%1),  %%r8, %%r14 ;" /* A[1]*A[0] */  "xorl %%r15d, %%r15d;"
//    "mulx 16(%1),  %%r9, %%r10 ;" /* A[2]*A[0] */  "adcx %%r14,  %%r9 ;"
//    "mulx 24(%1), %%rax, %%rcx ;" /* A[3]*A[0] */  "adcx %%rax, %%r10 ;"
//    "movq 24(%1), %%rdx        ;" /* A[3]      */
//    "mulx  8(%1), %%r11, %%r12 ;" /* A[1]*A[3] */  "adcx %%rcx, %%r11 ;"
//    "mulx 16(%1), %%rax, %%r13 ;" /* A[2]*A[3] */  "adcx %%rax, %%r12 ;"
//    "movq  8(%1), %%rdx        ;" /* A[1]      */  "adcx %%r15, %%r13 ;"
//    "mulx 16(%1), %%rax, %%rcx ;" /* A[2]*A[1] */  "movq    $0, %%r14 ;"
//    /*******************************************/  "adcx %%r15, %%r14 ;"

    Load64_buffer(rdx, inA_ptr,  0 + offset*8, Secret, inA_b, 0 + offset);     /* A[0] */
    /* The Xor64 clears the flags used for carry bits; NOTE: Original code uses xorl with r15d, maybe produces smaller code? */
    Mulx64(r14, r8, Mem(inA_ptr,  8 + offset*8, inA_b, 1 + offset, Secret));  /* A[0]*A[1] */ lemma_prod_bounds(r14,  r8, a0, a1);  Xor64(r15, r15);  

    Mulx64(r10, r9, Mem(inA_ptr, 16 + offset*8, inA_b, 2 + offset, Secret));  /* A[0]*A[2] */ lemma_prod_bounds(r10,  r9, a0, a2);  Adcx64Wrap( r9, r14);    
    
    Mulx64(rcx,rax, Mem(inA_ptr, 24 + offset*8, inA_b, 3 + offset, Secret));  /* A[0]*A[3] */ lemma_prod_bounds(rcx, rax, a0, a3);  Adcx64Wrap(r10, rax);    

    Load64_buffer(rdx, inA_ptr,  24 + offset*8, Secret, inA_b, 3 + offset);     /* A[3] */

    Mulx64(r12,r11, Mem(inA_ptr,  8 + offset*8, inA_b, 1 + offset, Secret));  /* A[3]*A[1] */ lemma_prod_bounds(r12, r11, a3, a1);  Adcx64Wrap(r11, rcx);

    Mulx64(r13,rax, Mem(inA_ptr, 16 + offset*8, inA_b, 2 + offset, Secret));  /* A[3]*A[2] */ lemma_prod_bounds(r13, rax, a3, a2);  Adcx64Wrap(r12, rax);    

    Load64_buffer(rdx, inA_ptr,   8 + offset*8, Secret, inA_b, 1 + offset);     /* A[1] */                                          Adcx64Wrap(r13, r15);  // r15 = 0, r13 < pow2_64 - 1, so this wraps the carry into r13

//    assert !cf(efl);

    Mulx64(rcx,rax, Mem(inA_ptr, 16 + offset*8, inA_b, 2 + offset, Secret));  /* A[1]*A[2] */ lemma_prod_bounds(rcx, rax, a1, a2);  Mov64(r14, 0);

//    assert r14 == 0;
//    assert r15 == 0;
//    Adcx64Wrap(r14, r15);     // <-- This appears to be redundant as cf, r14, and r15 are all provably 0 at this point

}

#reset-options "--z3rlimit 200000"
procedure{:quick} fast_sqr_part2()
    modifies
        rax; rcx; rdx; r8; r9; r10; r11; r12; r13; r14; r15;
        efl;

    requires
        adx_enabled && bmi2_enabled;
        r14 == 0;

    ensures
        pow2_seven(r8, r9, r10, r11, r12, r13, r14) ==
        old(pow2_six(2*r8, 2*r9, 2*(r10 + rax), 2*(r11+rcx), 2*r12, 2*r13));

        r14 <= 2;
{
    xor_lemmas();

    Xor64(r15, r15);
    Adox64Wrap(r10, rax);     Adcx64Wrap(r8, r8);
    Adox64Wrap(r11, rcx);     Adcx64Wrap(r9, r9);
    Adox64Wrap(r12, r15);     Adcx64Wrap(r10, r10);
    Adox64Wrap(r13, r15);     Adcx64Wrap(r11, r11);
    Adox64Wrap(r14, r15);     Adcx64Wrap(r12, r12);
                              Adcx64Wrap(r13, r13);
                              Adcx64Wrap(r14, r14);
}

#reset-options "--z3rlimit 400"
procedure{:quick} fast_sqr_part3(
    inline offset:nat,
    ghost dst_b:buffer64,
    ghost inA_b:buffer64)
    lets
        dst_ptr @= rdi;
        inA_ptr @= rsi;

        a0 := buffer64_read(inA_b, 0 + offset, mem);
        a1 := buffer64_read(inA_b, 1 + offset, mem);
        a2 := buffer64_read(inA_b, 2 + offset, mem);
        a3 := buffer64_read(inA_b, 3 + offset, mem);

        a := pow2_four(a0, a1, a2, a3);
    reads
        dst_ptr; inA_ptr; memTaint;

    modifies
        rax; rcx; rdx; r8; r9; r10; r11; r12; r13; r14; r15;
        mem; efl;

    requires
        adx_enabled && bmi2_enabled;
        offset == 0 || offset == 4;
        buffers_disjoint(dst_b, inA_b);

        r14 <= 2;

        validDstAddrs64(mem, dst_ptr, dst_b, 8 + offset*2, memTaint, Secret);
        validSrcAddrs64(mem, inA_ptr, inA_b, 4 + offset, memTaint, Secret);

    ensures
        let d0 := buffer64_read(dst_b, 0 + offset*2, mem);
        let d1 := buffer64_read(dst_b, 1 + offset*2, mem);
        let d2 := buffer64_read(dst_b, 2 + offset*2, mem);
        let d3 := buffer64_read(dst_b, 3 + offset*2, mem);
        let d4 := buffer64_read(dst_b, 4 + offset*2, mem);
        let d5 := buffer64_read(dst_b, 5 + offset*2, mem);
        let d6 := buffer64_read(dst_b, 6 + offset*2, mem);
        let d7 := buffer64_read(dst_b, 7 + offset*2, mem);
        
        pow2_nine(d0, d1, d2, d3, d4, d5, d6, d7, bool_bit(cf(efl))) ==
        old(pow2_eight(mul_nats(a0,a0), r8, mul_nats(a1,a1) + r9, r10, mul_nats(a2,a2) + r11, r12, mul_nats(a3,a3) + r13, r14));

        validSrcAddrs64(mem, dst_ptr, dst_b, 8 + offset*2, memTaint, Secret);
        modifies_buffer_specific(dst_b, old(mem), mem, 0 + offset*2, 8 + offset*2 - 1);
{
    xor_lemmas();

//    "movq   (%1), %%rdx ;"  "mulx %%rdx, %%rax, %%rcx ;" /* A[0]^2 */
//    /********************/  "movq %%rax,  0(%0) ;"
//    "addq %%rcx,  %%r8 ;"   "movq  %%r8,  8(%0) ;"
//    "movq  8(%1), %%rdx ;"  "mulx %%rdx, %%rax, %%rcx ;" /* A[1]^2 */
//    "adcq %%rax,  %%r9 ;"   "movq  %%r9, 16(%0) ;"
//    "adcq %%rcx, %%r10 ;"   "movq %%r10, 24(%0) ;"
//    "movq 16(%1), %%rdx ;"  "mulx %%rdx, %%rax, %%rcx ;" /* A[2]^2 */
//    "adcq %%rax, %%r11 ;"   "movq %%r11, 32(%0) ;"
//    "adcq %%rcx, %%r12 ;"   "movq %%r12, 40(%0) ;"
//    "movq 24(%1), %%rdx ;"  "mulx %%rdx, %%rax, %%rcx ;" /* A[3]^2 */
//    "adcq %%rax, %%r13 ;"   "movq %%r13, 48(%0) ;"
//    "adcq %%rcx, %%r14 ;"   "movq %%r14, 56(%0) ;"

    Load64_buffer(rdx, inA_ptr,  0 + offset*8, Secret, inA_b, 0 + offset);   Mulx64(rcx, rax, rdx);  /* A[0]^2 */                ghost var a0_sqr_hi := rcx; ghost var a0_sqr_lo := rax;  
    /********************/                               Store64_buffer(dst_ptr, rax,  0 + offset*16, Secret, dst_b, 0 + offset*2);
    Add64Wrap(r8, rcx);                                  Store64_buffer(dst_ptr,  r8,  8 + offset*16, Secret, dst_b, 1 + offset*2);
    Load64_buffer(rdx, inA_ptr,  8 + offset*8, Secret, inA_b, 1 + offset);   Mulx64(rcx, rax, rdx);  /* A[1]^2 */                ghost var a1_sqr_hi := rcx; ghost var a1_sqr_lo := rax;  
    Adcx64Wrap( r9, rax);                                Store64_buffer(dst_ptr,  r9, 16 + offset*16, Secret, dst_b, 2 + offset*2);
    Adcx64Wrap(r10, rcx);                                Store64_buffer(dst_ptr, r10, 24 + offset*16, Secret, dst_b, 3 + offset*2);
    Load64_buffer(rdx, inA_ptr, 16 + offset*8, Secret, inA_b, 2 + offset);   Mulx64(rcx, rax, rdx);  /* A[2]^2 */                ghost var a2_sqr_hi := rcx; ghost var a2_sqr_lo := rax;  
    Adcx64Wrap(r11, rax);                                Store64_buffer(dst_ptr, r11, 32 + offset*16, Secret, dst_b, 4 + offset*2);
    Adcx64Wrap(r12, rcx);                                Store64_buffer(dst_ptr, r12, 40 + offset*16, Secret, dst_b, 5 + offset*2);
    Load64_buffer(rdx, inA_ptr, 24 + offset*8, Secret, inA_b, 3 + offset);   Mulx64(rcx, rax, rdx);  /* A[3]^2 */       lemma_prod_bounds(rcx, rax, rdx, rdx);                ghost var a3_sqr_hi := rcx; ghost var a3_sqr_lo := rax;  
    Adcx64Wrap(r13, rax);                                Store64_buffer(dst_ptr, r13, 48 + offset*16, Secret, dst_b, 6 + offset*2);
    Adcx64Wrap(r14, rcx);                                Store64_buffer(dst_ptr, r14, 56 + offset*16, Secret, dst_b, 7 + offset*2);

    lemma_sqr_part3(a, a0, a1, a2, a3, a0_sqr_hi, a0_sqr_lo, a1_sqr_hi, a1_sqr_lo, a2_sqr_hi, a2_sqr_lo, a3_sqr_hi, a3_sqr_lo,
                    old(r8), old(r9), old(r10), old(r11), old(r12), old(r13), old(r14), 
                    buffer64_read(dst_b, 0 + offset*2, mem),
                    buffer64_read(dst_b, 1 + offset*2, mem),
                    buffer64_read(dst_b, 2 + offset*2, mem),
                    buffer64_read(dst_b, 3 + offset*2, mem),
                    buffer64_read(dst_b, 4 + offset*2, mem),
                    buffer64_read(dst_b, 5 + offset*2, mem),
                    buffer64_read(dst_b, 6 + offset*2, mem),
                    buffer64_read(dst_b, 7 + offset*2, mem),
                    bool_bit(cf(efl)));
}

#reset-options "--z3rlimit 600" 
procedure{:quick}{:public} fast_sqr(
    inline offset:nat,
    ghost dst_b:buffer64,
    ghost inA_b:buffer64)
    lets
        dst_ptr @= rdi;
        inA_ptr @= rsi;

        a0 := buffer64_read(inA_b, 0 + offset, mem);
        a1 := buffer64_read(inA_b, 1 + offset, mem);
        a2 := buffer64_read(inA_b, 2 + offset, mem);
        a3 := buffer64_read(inA_b, 3 + offset, mem);

        a := pow2_four(a0, a1, a2, a3);
    reads
        dst_ptr; inA_ptr; memTaint;

    modifies
        rax; rcx; rdx; r8; r9; r10; r11; r12; r13; r14; r15;
        mem; efl;

    requires
        adx_enabled && bmi2_enabled;
        offset == 0 || offset == 4;
        buffers_disjoint(dst_b, inA_b);

        validDstAddrs64(mem, dst_ptr, dst_b, 8 + offset*2, memTaint, Secret);
        validSrcAddrs64(mem, inA_ptr, inA_b, 4 + offset, memTaint, Secret);

    ensures
        let d0 := buffer64_read(dst_b, 0 + offset*2, mem);
        let d1 := buffer64_read(dst_b, 1 + offset*2, mem);
        let d2 := buffer64_read(dst_b, 2 + offset*2, mem);
        let d3 := buffer64_read(dst_b, 3 + offset*2, mem);
        let d4 := buffer64_read(dst_b, 4 + offset*2, mem);
        let d5 := buffer64_read(dst_b, 5 + offset*2, mem);
        let d6 := buffer64_read(dst_b, 6 + offset*2, mem);
        let d7 := buffer64_read(dst_b, 7 + offset*2, mem);
        let d := pow2_eight(d0, d1, d2, d3, d4, d5, d6, d7);
        d == a * a;

        validSrcAddrs64(mem, dst_ptr, dst_b, 8 + offset*2, memTaint, Secret);
        modifies_buffer_specific(dst_b, old(mem), mem, 0 + offset*2, 8 + offset*2 - 1);
{
    assert_by_tactic(a*a == pow2_seven(mul_nats(a0,a0), 2*mul_nats(a0,a1), 2*mul_nats(a0,a2) + mul_nats(a1,a1), 2*(mul_nats(a0,a3)+mul_nats(a1,a2)), 2*mul_nats(a1,a3) + mul_nats(a2,a2), 2*mul_nats(a2,a3), mul_nats(a3,a3)), int_canon);      // PASSES
    fast_sqr_part1(offset, inA_b);

    ghost var old_r8  := r8;
    ghost var old_r9  := r9;
    ghost var old_r10 := r10;
    ghost var old_r11 := r11;
    ghost var old_r12 := r12;
    ghost var old_r13 := r13;
    ghost var old_rax := rax;
    ghost var old_rcx := rcx;

    fast_sqr_part2();

    ghost var mid_r8  := r8;
    ghost var mid_r9  := r9;
    ghost var mid_r10 := r10;
    ghost var mid_r11 := r11;
    ghost var mid_r12 := r12;
    ghost var mid_r13 := r13;
    ghost var mid_r14 := r14;

    fast_sqr_part3(offset, dst_b, inA_b);

    ghost var d0 := buffer64_read(dst_b, 0 + offset*2, mem);
    ghost var d1 := buffer64_read(dst_b, 1 + offset*2, mem);
    ghost var d2 := buffer64_read(dst_b, 2 + offset*2, mem);
    ghost var d3 := buffer64_read(dst_b, 3 + offset*2, mem);
    ghost var d4 := buffer64_read(dst_b, 4 + offset*2, mem);
    ghost var d5 := buffer64_read(dst_b, 5 + offset*2, mem);
    ghost var d6 := buffer64_read(dst_b, 6 + offset*2, mem);
    ghost var d7 := buffer64_read(dst_b, 7 + offset*2, mem);
    lemma_sqr(a, a0, a1, a2, a3, 
              old_r8, old_r9, old_r10, old_r11, old_r12, old_r13, old_rax, old_rcx,
              mid_r8, mid_r9, mid_r10, mid_r11, mid_r12, mid_r13, mid_r14,
              d0, d1, d2, d3, d4, d5, d6, d7, bool_bit(cf(efl)));
}


#reset-options "--z3rlimit 40"

procedure{:quick}{:public} fast_sqr_stdcall(
    inline win:bool,
    ghost dst_b:buffer64,
    ghost inA_b:buffer64)
    lets
        dst_ptr @= rdi; inA_ptr @= rsi; 
        dst_in := (if win then rcx else rdi);
        inA_in := (if win then rdx else rsi);
    reads memTaint;
    modifies
        rax; rbx; rcx; rdx; rdi; rsi; r8; r9; r10; r11; r12; r13; r14; r15;
        rsp; efl; mem; stack;
    requires
        rsp == init_rsp(stack);
        adx_enabled && bmi2_enabled;
        buffers_disjoint(dst_b, inA_b);

        validDstAddrs64(mem, dst_in, dst_b, 8, memTaint, Secret);
        validSrcAddrs64(mem, inA_in, inA_b, 4, memTaint, Secret);
    ensures
        let a0 := old(buffer64_read(inA_b, 0, mem));
        let a1 := old(buffer64_read(inA_b, 1, mem));
        let a2 := old(buffer64_read(inA_b, 2, mem));
        let a3 := old(buffer64_read(inA_b, 3, mem));

        let d0 := buffer64_read(dst_b, 0, mem);
        let d1 := buffer64_read(dst_b, 1, mem);
        let d2 := buffer64_read(dst_b, 2, mem);
        let d3 := buffer64_read(dst_b, 3, mem);
        let d4 := buffer64_read(dst_b, 4, mem);
        let d5 := buffer64_read(dst_b, 5, mem);
        let d6 := buffer64_read(dst_b, 6, mem);
        let d7 := buffer64_read(dst_b, 7, mem);

        let a := a0 + pow2_64 * a1 + pow2_128 * a2 + pow2_192 * a3;

        let d := d0 + pow2_64 * d1 + pow2_128 * d2 + pow2_192 * d3 +
                 pow2_256 * d4 + pow2_320 * d5 + pow2_384 * d6 + pow2_448 * d7;
        d == a * a;

        //////////////////////////////////////
        //   Framing
        //////////////////////////////////////

        modifies_buffer(dst_b, old(mem), mem);
        validSrcAddrs64(mem, dst_in, dst_b, 8, memTaint, Secret);

        rbx == old(rbx);
        rsi == old(rsi);
        r12 == old(r12);
        r13 == old(r13);
        r14 == old(r14);
        r15 == old(r15);

        rsp == old(rsp);
{
    // Store callee-save registers
    Push(r15);
    Push(rsi);
    Push(r12);
    Push(r13);
    Push(r14);

    // Line up the rest of the arguments
    inline if (win) {
        Mov64(dst_ptr, rcx);
        Mov64(inA_ptr, rdx);
    } 

    fast_sqr(0, dst_b, inA_b);

    Pop(r14);
    Pop(r13);
    Pop(r12);
    Pop(rsi);
    Pop(r15);
}

/*
#reset-options "--z3rlimit 60" 
procedure{:quick} fast_sqr_loop(
    inline offset:nat,
    ghost dst_b:buffer64,
    ghost inA_b:buffer64)
    lets
        dst_ptr @= rdi;
        inA_ptr @= rsi;
        count   @= rbx;

        a0 := buffer64_read(inA_b, 0 + offset, mem);
        a1 := buffer64_read(inA_b, 1 + offset, mem);
        a2 := buffer64_read(inA_b, 2 + offset, mem);
        a3 := buffer64_read(inA_b, 3 + offset, mem);

        a := pow2_four(a0, a1, a2, a3);
    reads
        dst_ptr; inA_ptr; memTaint;

    modifies
        rax; rbx; rcx; rdx; r8; r9; r10; r11; r12; r13; r14; r15;
        mem; efl;

    requires
        adx_enabled && bmi2_enabled;
        offset == 0 || offset == 4;
        buffers_disjoint(dst_b, inA_b);

        validDstAddrs64(mem, dst_ptr, dst_b, 8 + offset*2, memTaint, Secret);
        validSrcAddrs64(mem, inA_ptr, inA_b, 4 + offset, memTaint, Secret);

    ensures
        let d0 := buffer64_read(dst_b, 0 + offset*2, mem);
        let d1 := buffer64_read(dst_b, 1 + offset*2, mem);
        let d2 := buffer64_read(dst_b, 2 + offset*2, mem);
        let d3 := buffer64_read(dst_b, 3 + offset*2, mem);
        let d4 := buffer64_read(dst_b, 4 + offset*2, mem);
        let d5 := buffer64_read(dst_b, 5 + offset*2, mem);
        let d6 := buffer64_read(dst_b, 6 + offset*2, mem);
        let d7 := buffer64_read(dst_b, 7 + offset*2, mem);
        let d := pow2_eight(d0, d1, d2, d3, d4, d5, d6, d7);
        //d == a * a;

        validSrcAddrs64(mem, dst_ptr, dst_b, 8 + offset*2, memTaint, Secret);
        modifies_buffer_specific(dst_b, old(mem), mem, 0 + offset*2, 8 + offset*2 - 1);
{
    while (count > 0) 
        invariant
            0 <= count <= old(count);

            adx_enabled && bmi2_enabled;
            offset == 0 || offset == 4;
            buffers_disjoint(dst_b, inA_b);

            validDstAddrs64(mem, dst_ptr, dst_b, 8 + offset*2, memTaint, Secret);
            validSrcAddrs64(mem, inA_ptr, inA_b, 4 + offset, memTaint, Secret);
        
            modifies_buffer_specific(dst_b, old(mem), mem, 0 + offset*2, 8 + offset*2 - 1);
        decreases count;
    {
        fast_sqr(offset, dst_b, inA_b);
        Sub64(count, 1);
    }
}

#reset-options "--z3rlimit 40"

procedure{:quick}{:public} fast_sqr_loop_stdcall(
    inline win:bool,
    ghost dst_b:buffer64,
    ghost inA_b:buffer64)
    lets
        dst_ptr @= rdi; inA_ptr @= rsi; count @= rbx;
        dst_in   := (if win then rcx else rdi);
        inA_in   := (if win then rdx else rsi);
        count_in := (if win then  r8 else rdx);
    reads memTaint;
    modifies
        rax; rbx; rcx; rdx; rdi; rsi; r8; r9; r10; r11; r12; r13; r14; r15;
        rsp; efl; mem; stack;
    requires
        rsp == init_rsp(stack);
        adx_enabled && bmi2_enabled;
        buffers_disjoint(dst_b, inA_b);

        validDstAddrs64(mem, dst_in, dst_b, 8, memTaint, Secret);
        validSrcAddrs64(mem, inA_in, inA_b, 4, memTaint, Secret);
    ensures
        let a0 := old(buffer64_read(inA_b, 0, mem));
        let a1 := old(buffer64_read(inA_b, 1, mem));
        let a2 := old(buffer64_read(inA_b, 2, mem));
        let a3 := old(buffer64_read(inA_b, 3, mem));

        let d0 := buffer64_read(dst_b, 0, mem);
        let d1 := buffer64_read(dst_b, 1, mem);
        let d2 := buffer64_read(dst_b, 2, mem);
        let d3 := buffer64_read(dst_b, 3, mem);
        let d4 := buffer64_read(dst_b, 4, mem);
        let d5 := buffer64_read(dst_b, 5, mem);
        let d6 := buffer64_read(dst_b, 6, mem);
        let d7 := buffer64_read(dst_b, 7, mem);

        let a := a0 + pow2_64 * a1 + pow2_128 * a2 + pow2_192 * a3;

        let d := d0 + pow2_64 * d1 + pow2_128 * d2 + pow2_192 * d3 +
                 pow2_256 * d4 + pow2_320 * d5 + pow2_384 * d6 + pow2_448 * d7;
        d == a * a;

        //////////////////////////////////////
        //   Framing
        //////////////////////////////////////

        modifies_buffer(dst_b, old(mem), mem);
        validSrcAddrs64(mem, dst_in, dst_b, 8, memTaint, Secret);

        rbx == old(rbx);
        rsi == old(rsi);
        r12 == old(r12);
        r13 == old(r13);
        r14 == old(r14);
        r15 == old(r15);

        rsp == old(rsp);
{
    // Store callee-save registers
    Push(rbx);
    Push(r15);
    Push(rsi);
    Push(r12);
    Push(r13);
    Push(r14);

    // Line up the rest of the arguments
    inline if (win) {
        Mov64(dst_ptr, rcx);
        Mov64(inA_ptr, rdx);
        Mov64(count, r8);
    } else {
        Mov64(count, rdx);
    }

    fast_sqr(0, dst_b, inA_b);

    Pop(r14);
    Pop(r13);
    Pop(r12);
    Pop(rsi);
    Pop(r15);
    Pop(rbx);
}
*/

procedure{:quick}{:public} sqr2_stdcall(
    inline win:bool,
    ghost dst_b:buffer64,
    ghost inA_b:buffer64)
    lets
        dst_ptr @= rdi; inA_ptr @= rsi; 
        dst_in := (if win then rcx else rdi);
        inA_in := (if win then rdx else rsi);
    reads memTaint;
    modifies
        rax; rbx; rcx; rdx; rdi; rsi; r8; r9; r10; r11; r12; r13; r14; r15;
        rsp; efl; mem; stack;
    requires
        rsp == init_rsp(stack);
        adx_enabled && bmi2_enabled;
        buffers_disjoint(dst_b, inA_b);

        validDstAddrs64(mem, dst_in, dst_b, 16, memTaint, Secret);
        validSrcAddrs64(mem, inA_in, inA_b, 8, memTaint, Secret);
    ensures
        let a0 := old(buffer64_read(inA_b, 0, mem));
        let a1 := old(buffer64_read(inA_b, 1, mem));
        let a2 := old(buffer64_read(inA_b, 2, mem));
        let a3 := old(buffer64_read(inA_b, 3, mem));

        let d0 := buffer64_read(dst_b, 0, mem);
        let d1 := buffer64_read(dst_b, 1, mem);
        let d2 := buffer64_read(dst_b, 2, mem);
        let d3 := buffer64_read(dst_b, 3, mem);
        let d4 := buffer64_read(dst_b, 4, mem);
        let d5 := buffer64_read(dst_b, 5, mem);
        let d6 := buffer64_read(dst_b, 6, mem);
        let d7 := buffer64_read(dst_b, 7, mem);

        let a := a0 + pow2_64 * a1 + pow2_128 * a2 + pow2_192 * a3;

        let d := d0 + pow2_64 * d1 + pow2_128 * d2 + pow2_192 * d3 +
                 pow2_256 * d4 + pow2_320 * d5 + pow2_384 * d6 + pow2_448 * d7;
        d == a * a;

        //////////////////////////////////////
        //   Framing
        //////////////////////////////////////

        modifies_buffer(dst_b, old(mem), mem);
        validSrcAddrs64(mem, dst_in, dst_b, 16, memTaint, Secret);

        rbx == old(rbx);
        rsi == old(rsi);
        r12 == old(r12);
        r13 == old(r13);
        r14 == old(r14);
        r15 == old(r15);

        rsp == old(rsp);
{
    // Store callee-save registers
    Push(r15);
    Push(r12);
    Push(r13);
    Push(r14);

    // Line up the rest of the arguments
    inline if (win) {
        Push(rsi);
        Mov64(dst_ptr, rcx);
        Mov64(inA_ptr, rdx);
    } 

    fast_sqr(0, dst_b, inA_b);
    fast_sqr(4, dst_b, inA_b);
    
    inline if (win) {
        Pop(rsi);
    }

    Pop(r14);
    Pop(r13);
    Pop(r12);
    Pop(r15);
}
